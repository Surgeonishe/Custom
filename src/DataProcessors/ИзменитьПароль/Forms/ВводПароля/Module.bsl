
&НаСервере
Процедура ПриСозданииНаСервере ( Отказ, СтандартнаяОбработка )
	
	Параметры.Свойство ( "ИДПользователя", ИДПользователя );
	Если ( ИДПользователя <> Неопределено И ЗначениеЗаполнено ( ИДПользователя ) ) Тогда
		пользователь = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору ( ИДПользователя );
		ПользовательИБ = пользователь.Имя;
	Иначе
		УстановитьПривилегированныйРежим ( ИСТИНА );
		пользователь = ПользователиИнформационнойБазы.ТекущийПользователь ();
		ПользовательИБ = пользователь.Имя;
		УстановитьПривилегированныйРежим ( ЛОЖЬ );		
	КонецЕсли;
	ТекущийПользователь = Параметры.ТекущийПользователь; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОК ( Команда )
	
	Если ( Пароль <> Подтверждение ) Тогда
		Элементы.Декорация.Видимость = ИСТИНА;
		Подтверждение = "";
		ТекущийЭлемент = Элементы.Подтверждение;
		Возврат;
	Иначе
		Элементы.Декорация.Видимость = ЛОЖЬ;
	КонецЕсли; 
	установитьПароль ();
	Если ( ТекущийПользователь ) Тогда
		ПоказатьВопрос ( Новый ОписаниеОповещения ( "ОбработкаОтвета", ЭтотОбъект ), "Перезапустить сейчас?", РежимДиалогаВопрос.ДаНет, , , , );	
	Иначе
		ПоказатьОповещениеПользователя ( "Пользователь - " + ПользовательИБ, , "Пароль изменен!", БиблиотекаКартинок.Замок );   
		Закрыть ();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтвета ( Результат, Парам ) Экспорт
	
	Если ( Результат = КодВозвратаДиалога.Да ) Тогда
		ЗавершитьРаботуСистемы ( , ИСТИНА );
	Иначе
		Закрыть ();
	КонецЕсли; 
	
КонецПроцедуры 

&НаСервере
Процедура установитьПароль ()
	
	УстановитьПривилегированныйРежим ( ИСТИНА );
	пользователь = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору ( ИДПользователя );
	пользователь.Пароль = Пароль;
	пользователь.Записать ();
	УстановитьПривилегированныйРежим ( ЛОЖЬ );
	
КонецПроцедуры