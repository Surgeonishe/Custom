
Функция Проведение ( Данные ) Экспорт
	
	получитьДанныеДокумента ( Данные );
	сформироватьПодотчет ( Данные );
	Если ( Данные.Шапка.ВидОперации = Перечисления.ВидОперацииАвансовогоОтчета.Возврат ) Тогда
		сформироватьДенежныеСредства ( Данные );	
		сформироватьДвиженияДенежныхСредств ( Данные );
	КонецЕсли; 
	установитьФлагиЗаписи ( Данные );
	Возврат ИСТИНА;
	
КонецФункции

Процедура получитьДанныеДокумента ( Данные )
	
	текст = "
	|// ~Шапка
	|ВЫБРАТЬ Ссылка как Ссылка, Номер как Номер, ДатаОперации как Период, ДатаОперации как ДатаОперации,
	|		 Касса как Касса, Описание как Описание, Сумма как Сумма, Сотрудник как Сотрудник,
	|		 Организация как Организация, Комментарий как Комментарий, Ответственный как Ответственный,
	|		 Валюта как Валюта, ВидОперации как ВидОперации, СтатьяДДС как СтатьяДДС
	|ИЗ 	 Документ.АвансовыйОтчет
	|ГДЕ	 Ссылка = &Ссылка
	|";
	запрос = Новый Запрос ( текст );
	запрос.УстановитьПараметр ( "Ссылка", Данные.Ссылка );
	Данные.Вставить ( "Шапка", запрос.Выполнить ().Выгрузить () [ 0 ] );

КонецПроцедуры 

Процедура сформироватьПодотчет ( Данные )
	
	движение = Данные.Движения.Подотчет.ДобавитьРасход ();
	движение.Период = Данные.Шапка.Период;
	движение.Сотрудник = Данные.Шапка.Сотрудник;
	движение.Валюта = Данные.Шапка.Валюта;
	движение.Сумма = Данные.Шапка.Сумма;
	
КонецПроцедуры

Процедура сформироватьДенежныеСредства ( Данные )
	
	движение = Данные.Движения.ДенежныеСредства.ДобавитьПриход ();
	движение.Период = Данные.Шапка.Период;
	движение.Казна = Данные.Шапка.Касса;
	движение.Валюта = Данные.Шапка.Валюта;
	движение.Сумма = Данные.Шапка.Сумма;
	
КонецПроцедуры

Процедура сформироватьДвиженияДенежныхСредств ( Данные )
	
	движение = Данные.Движения.ДвиженияДенежныхСредств.Добавить ();
	движение.Период = Данные.Шапка.Период;
	движение.Казна = Данные.Шапка.Касса;
	движение.Валюта = Данные.Шапка.Валюта;
	движение.Организация = Данные.Шапка.Организация;
	движение.СтатьяДДС = Данные.Шапка.СтатьяДДС;
	движение.СуммаПриход = Данные.Шапка.Сумма;
	движение.СуммаРасход = 0;
	
КонецПроцедуры

Процедура установитьФлагиЗаписи ( Данные )
	
	Данные.Движения.Подотчет.Записывать = ИСТИНА;
	Данные.Движения.ДенежныеСредства.Записывать = ИСТИНА;
	Данные.Движения.ДвиженияДенежныхСредств.Записывать = ИСТИНА;
	
КонецПроцедуры