
&НаСервере
Процедура ПриСозданииНаСервере ( Отказ, СтандартнаяОбработка )
	
	Если ( Объект.Ссылка.Пустая () ) Тогда
		начальноеЗаполнение ();
	КонецЕсли;
	ОбщийДокументы.СоздатьРеквизитыКомментарий ( ЭтаФорма, "Транспорт" );
	РежимРедактированияКомментарияПоСтроке = Ложь;
	ТекущаяКолонка = "";
	ТекущийИндекс = - 1;
	АктивацияПоКоманде = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура начальноеЗаполнение ()
	
	Объект.Ответственный = ПараметрыСеанса.ТекущийПользователь;
	Объект.Валюта = Справочники.Валюты.Доллар; 
	Объект.Курс = получитьКурс ( ? ( Объект.Ссылка.Пустая (), ТекущаяДата (), Объект.Дата ), Объект.Валюта );
	Если (  ЗначениеЗаполнено ( Параметры.Основание ) ) Тогда
		// код ...
	Иначе
		Объект.Организация = Константы.ОсновнаяОрганизация.Получить ();			
	КонецЕсли; 
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция получитьКурс ( Дата, Валюта )
	
	курсы = ОбщийДокументы.ПолучитьКурс ( Дата, Валюта );
	Возврат курсы.Курс;

КонецФункции 

&НаКлиенте
Процедура ПриОткрытии ( Отказ )
	
	ОбщийДокументы.ЗаполнитьРеквизитКомментарий ( ЭтаФорма, "Транспорт" );
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоГТД ( Команда )
	
	Если ( ЗначениеЗаполнено ( Объект.ГТД ) ) Тогда
		Если ( Объект.Транспорт.Количество () > 0 ) Тогда
			ПоказатьВопрос ( Новый ОписаниеОповещения ( "ВопросЗаполнитьПоГТД", ЭтотОбъект ), "Очистить и заполнить табличную часть ""Транспорт""?", РежимДиалогаВопрос.ДаНетОтмена );
		Иначе
			заполнитьПоГТДСервер ();
		КонецЕсли;
	Иначе
		Сообщить ( "Не выбран документ ""ГТД""!", СтатусСообщения.Важное );
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросЗаполнитьПоГТД ( Ответ, Парам ) Экспорт
	
	Если ( Ответ = КодВозвратаДиалога.Отмена ) Тогда
		Возврат;
	ИначеЕсли ( Ответ = КодВозвратаДиалога.Да ) Тогда
		Объект.Транспорт.Очистить (); 
	КонецЕсли;
	заполнитьПоГТДСервер ();
	
КонецПроцедуры 

&НаСервере
Процедура заполнитьПоГТДСервер ()
	
	с = "
	|выбрать
	|	Контрагент как Контрагент, Номенклатура как Номенклатура, Транспорт как Транспорт
	|из
	|	Документ.ГТД.Транспорт
	|где
	|	Ссылка = &ГТД
	|упорядочить по
	|	НомерСтроки 
	|";
	запрос = Новый Запрос ( с );
	запрос.УстановитьПараметр ( "ГТД", Объект.ГТД );
	результат = запрос.Выполнить ();
	выборка = результат.Выбрать ();
	Пока ( выборка.Следующий () ) Цикл
		новаяСтрока = Объект.Транспорт.Добавить ();
		ЗаполнитьЗначенияСвойств ( новаяСтрока, выборка ); 
	КонецЦикла; 
	
КонецПроцедуры 

&НаКлиенте
Процедура ТранспортСуммаПриИзменении ( Элемент )
	
	рассчитатьСвязанноеПоле ( Элемент.Имя );
	
КонецПроцедуры

&НаКлиенте
Процедура рассчитатьСвязанноеПоле ( Знач ИмяЭлемента )
	
	текущиеДанные = Элементы.Транспорт.ТекущиеДанные;
	рассчитатьИтоги ( текущиеДанные );
	
КонецПроцедуры 

&НаКлиенте
Процедура рассчитатьИтоги ( Данные )
	
	Данные.СуммаИтого = Данные.СуммаАвто + Данные.СуммаВесовка + Данные.СуммаДемередж
						+ Данные.СуммаДопРасходы + Данные.СуммаПортовскиеРасходы + Данные.СуммаПРР
						+ Данные.СуммаСтраховка + Данные.СуммаФракт + Данные.СуммаХранение;
	Данные.СуммаИтогоДоллар = Данные.СуммаАвтоДоллар + Данные.СуммаВесовкаДоллар + Данные.СуммаДемереджДоллар
						+ Данные.СуммаДопРасходыДоллар + Данные.СуммаПортовскиеРасходыДоллар + Данные.СуммаПРРДоллар
						+ Данные.СуммаСтраховкаДоллар + Данные.СуммаФрактДоллар + Данные.СуммаХранениеДоллар;
	Объект.СуммаПоДокументу = 0;
	Объект.СуммаПоДокументуДоллар = 0;
	Для Каждого строкаТЧ Из Объект.Транспорт Цикл
		Объект.СуммаПоДокументу = Объект.СуммаПоДокументу + строкаТЧ.СуммаИтого;
		Объект.СуммаПоДокументуДоллар = Объект.СуммаПоДокументуДоллар + строкаТЧ.СуммаИтогоДоллар;  
	КонецЦикла; 
	
КонецПроцедуры 

&НаКлиенте
Процедура ГТДПриИзменении ( Элемент )
	
	Если ( ЗначениеЗаполнено ( Объект.Контроль ) ) Тогда
		ПоказатьВопрос ( Новый ОписаниеОповещения ( "ВопросОЗаполнении", ЭтотОбъект ), "Заполнить на основании документа ""Контроль""?", РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Нет );
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросОЗаполнении ( Ответ, Парам ) Экспорт
	
	Если ( Ответ = КодВозвратаДиалога.Да ) Тогда
		заполнитьНаСервере ();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура заполнитьНаСервере ()
	
	_объект = РеквизитФормыВЗначение ( "Объект" );
	_объект.ОбработкаЗаполнения ( Объект.Контроль, Истина );
	ЗначениеВРеквизитФормы ( _объект, "Объект" );
	
КонецПроцедуры 

&НаКлиенте
Процедура ДатаПриИзменении ( Элемент )
	
	Объект.Курс = получитьКурс ( Объект.Дата, Объект.Валюта );
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортПриНачалеРедактирования ( Элемент, НоваяСтрока, Копирование )
	
	// код ... 
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКомментарий ( Команда )
	
	Модифицированность = Истина;
	Если ( ТекущийИндекс = - 1 ИЛИ Элементы.Транспорт.ТекущаяСтрока = Неопределено ) Тогда
		РежимРедактированияКомментарияПоСтроке = Ложь;
	Иначе
		Если ( РежимРедактированияКомментарияПоСтроке ) Тогда
			изменитьКомментарий ();
		Иначе
			ТекущийЭлемент = Элементы.КомментарийПоСтроке;
		КонецЕсли; 
		РежимРедактированияКомментарияПоСтроке = НЕ РежимРедактированияКомментарияПоСтроке;
	КонецЕсли; 
	изменитьОформлениеКомментария ();
	АктивацияПоКоманде = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура изменитьКомментарий ()
	
	данные = Элементы.Транспорт.ТекущиеДанные;
	Если ( данные <> Неопределено И ТекущийИндекс >= 0 И ТекущаяКолонка <> "" ) Тогда
		данные [ ТекущаяКолонка + "Комментарий" ] = КомментарийПоСтроке;          
	КонецЕсли; 
	
КонецПроцедуры  

&НаКлиенте
Процедура изменитьОформлениеКомментария ()
	
	Элементы.КомментарийПоСтроке.ТолькоПросмотр = НЕ РежимРедактированияКомментарияПоСтроке;
	Если ( РежимРедактированияКомментарияПоСтроке ) Тогда
		Элементы.ДобавитьКомментарий.Картинка = БиблиотекаКартинок.ОформлениеЗнакФлажок;
		Элементы.КомментарийПоСтроке.ЦветФона = Новый Цвет ( 255, 250, 217 );
	Иначе
		Элементы.ДобавитьКомментарий.Картинка = БиблиотекаКартинок.Изменить;
		Элементы.КомментарийПоСтроке.ЦветФона = Новый Цвет ( 247, 247, 247 );
	КонецЕсли; 	
	
КонецПроцедуры  

&НаКлиенте
Процедура УдалитьКомментарий ( Команда )
	
	Модифицированность = Истина;
	данные = Элементы.Транспорт.ТекущиеДанные;
	Если ( данные <> Неопределено И ТекущийИндекс >= 0 И ТекущаяКолонка <> "" ) Тогда
		данные [ ТекущаяКолонка + "Комментарий" ] = "";
		КомментарийПоСтроке = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортПриАктивизацииПоля ( Элемент )
	
	//Если ( РежимРедактированияКомментарияПоСтроке И НЕ АктивацияПоКоманде ) Тогда
	//	РежимРедактированияКомментарияПоСтроке = Ложь;
	//	изменитьОформлениеКомментария ();
	//КонецЕсли;
	АктивацияПоКоманде = Ложь;
	имя = Элементы.Транспорт.ТекущийЭлемент.Имя;
	Если ( СтрНайти ( имя, "ТранспортСумма" ) > 0 ) Тогда
		данные = Элементы.Транспорт.ТекущиеДанные;
		Если ( данные <> Неопределено ) Тогда
			реквизит = СтрЗаменить ( имя, "Транспорт", "" );
			КомментарийПоСтроке = данные [ реквизит  + "Комментарий" ];
		КонецЕсли; 
		установитьДоступность ( Истина );
		ТекущийИндекс = Объект.Транспорт.НайтиПоИдентификатору ( Элементы.Транспорт.ТекущаяСтрока );
		ТекущаяКолонка = реквизит;
	Иначе
 		КомментарийПоСтроке = "";
		ТекущийИндекс = - 1;
		ТекущаяКолонка = "";
		установитьДоступность ( Ложь );
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура установитьДоступность ( Значение )
	
	Элементы.ДобавитьКомментарий.Доступность = Значение;
	Элементы.УдалитьКомментарий.Доступность = Значение;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью ( Отказ, ПараметрыЗаписи )
	
	записатьКомментарииТранспорт ();	
	
КонецПроцедуры

&НаКлиенте
Процедура записатьКомментарииТранспорт ()
	
	м = получитьМассивКолонок ();
	Для Каждого строкаТЧ Из Объект.Транспорт Цикл
		строкаТЧ.Комментарий = получитьКомментарийПоСтроке ( строкаТЧ, м );	
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Функция получитьМассивКолонок ()
	
	м = Новый Массив ();
	м.Добавить ( "СуммаВесовка" );
	м.Добавить ( "СуммаВесовкаДоллар" );
	м.Добавить ( "СуммаДемередж" );
	м.Добавить ( "СуммаДемереджДоллар" );
	м.Добавить ( "СуммаДопРасходы" );
	м.Добавить ( "СуммаДопРасходыДоллар" );
	м.Добавить ( "СуммаДопРасходыКлиента" );
	м.Добавить ( "СуммаДопРасходыКлиентаДоллар" );
	м.Добавить ( "СуммаИтого" );
	м.Добавить ( "СуммаИтогоДоллар" );
	м.Добавить ( "СуммаПортовскиеРасходы" );
	м.Добавить ( "СуммаПортовскиеРасходыДоллар" );
	м.Добавить ( "СуммаПРР" );
	м.Добавить ( "СуммаПРРДоллар" );
	м.Добавить ( "СуммаСтраховка" );
	м.Добавить ( "СуммаСтраховкаДоллар" );
	м.Добавить ( "СуммаФракт" );
	м.Добавить ( "СуммаФрактДоллар" );
	м.Добавить ( "СуммаХранение" );
	м.Добавить ( "СуммаХранениеДоллар" );
	м.Добавить ( "СуммаАвто" );
	м.Добавить ( "СуммаАвтоДоллар" );
	Возврат м;

КонецФункции  

&НаКлиенте
Функция получитьКомментарийПоСтроке ( Данные, Колонки )
	
	м = Новый Массив ();
	комментарий = "";
	Для Каждого колонка Из Колонки Цикл
		коммент = Данные [ колонка + "Комментарий" ];
		Если ( коммент = Неопределено ИЛИ СокрЛП ( коммент ) = "" ) Тогда
			Продолжить;
		Иначе
			с = "##" + колонка + "##" + коммент;
			м.Добавить ( с );	
		КонецЕсли; 	
	КонецЦикла;
	Если ( м.Количество () > 0 ) Тогда
		комментарий = СтрСоединить ( м, "&&" );
	КонецЕсли;
	Возврат комментарий;

КонецФункции  

&НаКлиенте
Процедура КомментарийПоСтрокеПриИзменении ( Элемент )
	
	Модифицированность = Истина;
	изменитьКомментарий ();
	РежимРедактированияКомментарияПоСтроке = НЕ РежимРедактированияКомментарияПоСтроке;
 	изменитьОформлениеКомментария ();
	
КонецПроцедуры