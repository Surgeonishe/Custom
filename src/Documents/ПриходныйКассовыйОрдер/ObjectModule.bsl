
Процедура ПередЗаписью ( Отказ, РежимЗаписи, РежимПроведения )
	
	Если ( ОбменДанными.Загрузка ) Тогда
		Возврат;
	КонецЕсли;
	заполнитьОплаты ();		
	
КонецПроцедуры

Процедура заполнитьОплаты ()
	
	колвоСтрок = Оплаты.Количество ();
	Если ( колвоСтрок = 0 ) Тогда
		новая = Оплаты.Добавить ();
		новая.Валюта = Валюта;
		новая.Сумма = Сумма;
		новая.Курс = Курс;
	ИначеЕсли ( колвоСтрок = 1 ) Тогда 
		строкаТЧ = Оплаты [ 0 ];  
		Если ( Валюта = строкаТЧ.Валюта ) Тогда
			строкаТЧ.Курс = 1;
		КонецЕсли;
	Иначе
		// мультивалютный документ
	КонецЕсли; 
	
КонецПроцедуры 

Процедура заполнитьДополнительныеСвойства ()
	
	ДополнительныеСвойства.Вставить ( "Перепроведение", Проведен );
	ДополнительныеСвойства.Вставить ( "ОперативноеПроведение", ( ЭтоНовый () И НачалоДня ( Дата ) = НачалоДня ( ТекущаяДата () ) ) ); 
	
КонецПроцедуры 

Процедура ОбработкаПроведения ( Отказ, РежимПроведения )
	
	заполнитьДополнительныеСвойства	();
	данные = Новый Структура ();
	данные.Вставить ( "Ссылка", Ссылка );
	данные.Вставить ( "Дата", Дата );
	данные.Вставить ( "Движения", Движения );
	данные.Вставить ( "МоментВремени", МоментВремени () );
	данные.Вставить ( "ДополнительныеСвойства", ДополнительныеСвойства );
	Отказ = НЕ Документы.ПриходныйКассовыйОрдер.Проведение ( данные );
	
КонецПроцедуры

Процедура ОбработкаЗаполнения ( ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка )
	
	Если ТипЗнч ( ДанныеЗаполнения ) = Тип ( "ДокументСсылка.СчетНаОплату" ) Тогда
		заполнитьПоСчетНаОплату ( ДанныеЗаполнения );
	ИначеЕсли ТипЗнч ( ДанныеЗаполнения ) = Тип ( "ДокументСсылка.ГТД" ) Тогда
		заполнитьПоГТД ( ДанныеЗаполнения );
	ИначеЕсли ТипЗнч ( ДанныеЗаполнения ) = Тип ( "ДокументСсылка.РасходныйКассовыйОрдер" ) Тогда
		заполнитьПоРКО ( ДанныеЗаполнения );
	КонецЕсли;
	
КонецПроцедуры

Процедура заполнитьПоСчетНаОплату ( Данные ) 
	
	// шапка
	ГТД = Данные;
	с = "
	|выбрать
	|	Контрагент как Контрагент, Сумма как Сумма,
	| 	Организация как Организация, СуммаДоллар как СуммаДоллар,
	|	Ссылка как СчетНаОплату, НазначениеПлатежа как НазначениеПлатежа,
	|	представление ( Ссылка ) как ПредставлениеСчетНаОплату,
	|	ГТД как ГТД 
	|из
	|	Документ.СчетНаОплату
	|где
	|	Ссылка = &Данные  
	|";
	запрос = Новый Запрос ( с );
	запрос.УстановитьПараметр ( "Данные", Данные );
	результат = запрос.Выполнить ();
	выборка = результат.Выбрать ();
	выборка.Следующий ();
	Организация = выборка.Организация;
	Контрагент = выборка.Контрагент;
	Сумма = выборка.Сумма;
	Валюта = Справочники.Валюты.Гривна;
	СуммаДоллар = выборка.СуммаДоллар;
	СчетНаОплату = выборка.СчетНаОплату;
	ГТД = выборка.ГТД;
	НазначениеПлатежа = выборка.НазначениеПлатежа;
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
	Комментарий = "На основании счета на оплату - " + выборка.ПредставлениеСчетНаОплату;
	
КонецПроцедуры

Процедура заполнитьПоГТД ( Данные )
	
	// шапка
	ГТД = Данные;
	с = "
	|выбрать
	|	Контрагент как Контрагент, 0 как Сумма,
	| 	Организация как Организация, СуммаПоДокументу как СуммаДоллар,
	|	представление ( Ссылка ) как ПредставлениеГТД,
	|	Ссылка как ГТД, СуммаПоДокументу как СуммаДолгГТД
	|из
	|	Документ.ГТД
	|где
	|	Ссылка = &Данные  
	|";
	запрос = Новый Запрос ( с );
	запрос.УстановитьПараметр ( "Данные", Данные );
	результат = запрос.Выполнить ();
	выборка = результат.Выбрать ();
	выборка.Следующий ();
	Организация = выборка.Организация;
	Контрагент = выборка.Контрагент;
	Сумма = выборка.Сумма;
	СуммаДоллар = выборка.СуммаДоллар;
	СуммаДолгГТД = выборка.СуммаДолгГТД;
	ГТД = выборка.ГТД;
	НазначениеПлатежа = "";
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
	Комментарий = "На основании ГТД - " + выборка.ПредставлениеГТД;
	
КонецПроцедуры

Процедура заполнитьПоРКО ( Данные )
	
	с = "
	|выбрать
	|	Ссылка как Основание, 
	|	СтатьяДДС как СтатьяДДС,
	|	Контрагент как Контрагент, 
	|	Сумма как Сумма, 
	|	Валюта как Валюта,
	|	Касса как КассаПеремещение,
	|	КассаПеремещение как Касса, 
	|	Организация как Организация,
	|	ВидОперации как ВидОперации, 
	|	Дата как Дата 
	|из
	|	Документ.РасходныйКассовыйОрдер
	|где
	|	Ссылка = &Данные  
	|";
	запрос = Новый Запрос ( с );
	запрос.УстановитьПараметр ( "Данные", Данные );
	результат = запрос.Выполнить ();
	выборка = результат.Выбрать ();
	выборка.Следующий ();
	Если ( выборка.ВидОперации = Перечисления.ВидОперацииРасходныйКассовыйОрдер.ПеремещениеВКассу
		ИЛИ выборка.ВидОперации = Перечисления.ВидОперацииРасходныйКассовыйОрдер.Оплата ) Тогда
		заполнитьПоПеремещению ( выборка );	
	КонецЕсли;	
	
КонецПроцедуры

Процедура заполнитьПоПеремещению ( Выборка )
	
	Дата = Выборка.Дата;
	ВидОперации = Перечисления.ВидОперацииПриходныйКассовыйОрдер.ПеремещениеИзКассы;
	Организация = Выборка.Организация;
	СтатьяДДС = Справочники.СтатьяДДС.НайтиПоКоду ( "000000037" ); // подотчет
	Сумма = Выборка.Сумма;
	Валюта = Выборка.Валюта;
	КассаПеремещение = Выборка.КассаПеремещение;
	Касса = Выборка.Касса;
	НазначениеПлатежа = "";
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
	Контрагент = Выборка.Контрагент;
	Комментарий = "На основании - " + выборка.Основание;
	
КонецПроцедуры 