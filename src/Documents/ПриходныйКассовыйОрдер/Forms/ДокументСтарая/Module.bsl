
&НаСервере
Процедура ПриСозданииНаСервере ( Отказ, СтандартнаяОбработка )
	
	Если ( Объект.Ссылка.Пустая () ) Тогда
		начальноеЗаполнение ();
	Иначе
		// код ...	
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура начальноеЗаполнение ()
	
	ОбщийДокументы.НачальноеЗаполнение ( Объект );
	Объект.ВидОперации = Перечисления.ВидОперацииПриходныйКассовыйОрдер.ОплатаКлиента;
	Объект.Организация = Константы.ОсновнаяОрганизация.Получить ();
	Объект.Касса = Константы.ОсновнаяКасса.Получить (); 
	Если ( Параметры.ЗначенияЗаполнения.Свойство ( "ВидОперации" ) ) Тогда
		Если ( ЗначениеЗаполнено ( Параметры.ЗначенияЗаполнения.ВидОперации ) ) Тогда
			Объект.ВидОперации = Параметры.ЗначенияЗаполнения.ВидОперации;
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ПриОткрытии ( Отказ )
	
	установитьВидимостьСтраниц ();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОперацииПриИзменении ( Элемент )
	
	установитьВидимостьСтраниц ();	
	
КонецПроцедуры

&НаКлиенте
Процедура установитьВидимостьСтраниц ()
	
	Если ( Объект.ВидОперации = ПредопределенноеЗначение ( "Перечисление.ВидОперацииПриходныйКассовыйОрдер.ОплатаКлиента" ) ) Тогда
		Элементы.СтраницаОплатаКлиента.Видимость = ИСТИНА;
		Элементы.СтраницаПрочее.Видимость = ЛОЖЬ;
	ИначеЕсли ( Объект.ВидОперации = ПредопределенноеЗначение ( "Перечисление.ВидОперацииПриходныйКассовыйОрдер.Прочее" ) ) Тогда
		Элементы.СтраницаОплатаКлиента.Видимость = ЛОЖЬ;
		Элементы.СтраницаПрочее.Видимость = ИСТИНА;
	КонецЕсли; 
	
КонецПроцедуры 

&НаКлиенте
Процедура РаспределитьСуммуПоТранспорту ( Команда )
	
	колвоТранспорта = Объект.Транспорт.Количество ();
	Если ( колвоТранспорта = 0 ) Тогда
		ПоказатьПредупреждение ( , "Не заполнена табличная часть ""Транспорт""!" );
		Возврат;
	Иначе
		ПоказатьВопрос ( Новый ОписаниеОповещения ( "РаспределитьВопрос", ЭтотОбъект ), "Распределить суммы?", РежимДиалогаВопрос.ДаНетОтмена ); 
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределитьВопрос ( Ответ, Парам ) Экспорт
	
	Если ( Ответ = КодВозвратаДиалога.Нет ) Тогда
		Возврат;
	КонецЕсли;
	распределитьСуммы ();
	
КонецПроцедуры 

&НаКлиенте
Процедура распределитьСуммы ()
	
	колвоТранспорта = Объект.Транспорт.Количество ();
	Для Каждого строкаТЧ Из Объект.Транспорт Цикл
		строкаТЧ.Сумма = Объект.Сумма / колвоТранспорта;
		строкаТЧ.СуммаДоллар = Объект.СуммаДоллар / колвоТранспорта;
	КонецЦикла;	
	
КонецПроцедуры 

&НаКлиенте
Процедура ЗаполнитьТранспортГТД ( Команда )
	
	Если ( Объект.ГТД = ПредопределенноеЗначение ( "Документ.ГТД.ПустаяСсылка" ) ) Тогда
		ПоказатьПредупреждение ( , "Не выбран документ ""ГТД""!" );
		Возврат;
	КонецЕсли; 
	Если ( Объект.Транспорт.Количество () > 0 ) Тогда
		ПоказатьВопрос ( Новый ОписаниеОповещения ( "ЗаполнитьВопрос", ЭтотОбъект ), "Очистить табличную часть ""Транспорт"" и заполнить?", РежимДиалогаВопрос.ДаНетОтмена ); 
	Иначе
		заполнитьПоГТД ();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьВопрос ( Ответ, Парам ) Экспорт
	
	Если ( Ответ = КодВозвратаДиалога.Отмена ) Тогда
		Возврат;
	ИначеЕсли ( Ответ = КодВозвратаДиалога.Да ) Тогда 
		Объект.Транспорт.Очистить (); 
	КонецЕсли;
	заполнитьПоГТД ();
	
КонецПроцедуры

&НаКлиенте
Процедура заполнитьПоГТД ()
	
	мТранспорт = получитьТранспорт ( Объект.ГТД );
	Для Каждого транспорт Из мТранспорт Цикл
		новаяСтрока = Объект.Транспорт.Добавить ();
		новаяСтрока.Транспорт = транспорт;		
	КонецЦикла; 
	распределитьСуммы ();
	
КонецПроцедуры 

&НаСервереБезКонтекста
Функция получитьТранспорт ( ГТД )
	
	с = "
	|выбрать
	|	Транспорт как Транспорт
	|из
	|	Документ.ГТД.Транспорт
	|где
	|	Ссылка = &ГТД
	|упорядочить по
	|	НомерСтроки 
	|";
	запрос = Новый Запрос ( с );
	запрос.УстановитьПараметр ( "ГТД", ГТД );
	Возврат запрос.Выполнить ().Выгрузить ().ВыгрузитьКолонку ( "Транспорт" );
	
КонецФункции 