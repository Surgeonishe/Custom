
Функция Проведение ( Данные ) Экспорт
	
	получитьДанныеДокумента ( Данные );
	сформироватьВзаиморасчеты ( Данные );
	сформироватьДоходыРасходы ( Данные );
	установитьФлагиЗаписи ( Данные );
	Возврат ИСТИНА;
	
КонецФункции

Процедура получитьДанныеДокумента ( Данные )
	
	результат = ИСТИНА;
	стр = "
	|// ~Шапка
	|ВЫБРАТЬ
	|	Дата КАК Период, Контрагент КАК Контрагент, Пост КАК Пост, Сумма КАК Сумма,
	|	Валюта КАК Валюта, Заявка КАК Заявка, Организация КАК Организация
	|ИЗ Документ.ПоступлениеУслуг
	|ГДЕ Ссылка = &Ссылка
	|;
	|// ~Услуги
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Номенклатура.Родитель = значение ( Справочник.Номенклатура.ОформлениеФитосанитарногоСертификата )
	|			ТОГДА значение ( Справочник.Номенклатура.ОформлениеФитосанитарногоСертификата )
	|		ИНАЧЕ Номенклатура
	|	КОНЕЦ КАК Номенклатура, 
	|	СУММА ( Сумма ) КАК Сумма 
	|ИЗ Документ.ПоступлениеУслуг.Услуги
	|ГДЕ Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО 
	|	ВЫБОР
	|		КОГДА Номенклатура.Родитель = значение ( Справочник.Номенклатура.ОформлениеФитосанитарногоСертификата )
	|			ТОГДА значение ( Справочник.Номенклатура.ОформлениеФитосанитарногоСертификата )
	|		ИНАЧЕ Номенклатура
	|	КОНЕЦ
	|";
	запрос = Новый Запрос ( стр );
	запрос.УстановитьПараметр ( "Ссылка", Данные.Ссылка );
	результат = запрос.ВыполнитьПакет ();
	Данные.Вставить ( "Шапка", результат [ 0 ].Выгрузить () [ 0 ] );
	Данные.Вставить ( "Услуги", результат [ 1 ].Выгрузить () );
	
КонецПроцедуры

Процедура сформироватьВзаиморасчеты ( Данные )
	
	движение = Данные.Движения.Взаиморасчеты.ДобавитьПриход ();
	движение.Период = Данные.Шапка.Период;
	движение.Контрагент = Данные.Шапка.Контрагент;
	движение.Организация = Данные.Шапка.Организация;
	движение.Заявка = Данные.Шапка.Заявка;
	движение.Валюта = Данные.Шапка.Валюта;
	движение.Сумма = Данные.Шапка.Сумма;
	
КонецПроцедуры

Процедура сформироватьДоходыРасходы ( Данные )
	
	Для Каждого строкаТЧ Из Данные.Услуги Цикл
		движение = Данные.Движения.ДоходыРасходы.Добавить ();
		движение.Период = Данные.Шапка.Период;
		движение.Номенклатура = строкаТЧ.Номенклатура;
		движение.Заявка = Данные.Шапка.Заявка;
		движение.Валюта = Данные.Шапка.Валюта;
		движение.СуммаРасход = строкаТЧ.Сумма;
	КонецЦикла; 
	
КонецПроцедуры 

Процедура установитьФлагиЗаписи ( Данные )
	
	Данные.Движения.Взаиморасчеты.Записывать = ИСТИНА;
	Данные.Движения.ДоходыРасходы.Записывать = ИСТИНА;

КонецПроцедуры