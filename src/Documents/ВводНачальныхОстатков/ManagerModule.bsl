
Функция Проведение ( Данные ) Экспорт
	
	получитьДанныеДокумента ( Данные );
	сформироватьВзаиморасчеты ( Данные );
	сформироватьДенежныеСредства ( Данные );
	установитьФлагиЗаписи ( Данные );
	Возврат ИСТИНА;
	
КонецФункции

Процедура получитьДанныеДокумента ( Данные )
	
	с = "
	|// ~Шапка
	|выбрать 
	|	Ссылка как Ссылка, Дата как Дата, Организация как Организация
	|из
	|	Документ.ВводНачальныхОстатков
	|где
	|	Ссылка = &Ссылка
	|;
	|// ~Взаиморасчеты 
	|выбрать
	| 	Ссылка как Ссылка, ЗНАЧЕНИЕ ( ВидДвиженияНакопления.Приход ) как ВидДвижения,
	|	Ссылка.Дата как Период, Сумма как Сумма, Ссылка.Организация как Организация,
	|	Валюта как Валюта
	|из
	|	Документ.ВводНачальныхОстатков.Взаиморасчеты                                           
	|где                                        
	|	Ссылка = &Ссылка
	|;
	|// ~Денежные средства 
	|выбрать
	|	Ссылка.Дата как Период, Казна как Казна, Сумма как Сумма, Валюта как Валюта
	|из
	|	Документ.ВводНачальныхОстатков.ДенежныеСредства                                           
	|где
	|	Ссылка = &Ссылка 
	|";
	запрос = Новый Запрос ( с );
	запрос.УстановитьПараметр ( "Ссылка", Данные.Ссылка );
	результат = запрос.ВыполнитьПакет ();
	Данные.Вставить ( "Шапка", результат [ 0 ].Выгрузить () [ 0 ] );
	Данные.Вставить ( "Взаиморасчеты", результат [ 1 ].Выгрузить () );
	Данные.Вставить ( "ДенежныеСредства", результат [ 2 ].Выгрузить () );
	
КонецПроцедуры

Процедура сформироватьВзаиморасчеты ( Данные )
	
	Данные.Движения.Взаиморасчеты.Загрузить ( Данные.Взаиморасчеты );
	
КонецПроцедуры

Процедура сформироватьДенежныеСредства ( Данные )
	
	Данные.Движения.ДенежныеСредства.Загрузить ( Данные.ДенежныеСредства );
	
КонецПроцедуры

Процедура установитьФлагиЗаписи ( Данные )
	
	Данные.Движения.Взаиморасчеты.Записывать = ИСТИНА;
	Данные.Движения.ДенежныеСредства.Записывать = ИСТИНА;
	Данные.Движения.ДоходыРасходы.Записывать = ИСТИНА;
	
КонецПроцедуры