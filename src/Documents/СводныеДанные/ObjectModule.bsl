
Процедура ПередЗаписью ( Отказ, РежимЗаписи, РежимПроведения )
	
	Если ( ОбменДанными.Загрузка ) Тогда
		Возврат;
	КонецЕсли;
	Количество = Транспорт.Итог ( "Количество" );
	КоличествоТранспорта = Транспорт.Количество ();
	СуммаИтогДоллар = Транспорт.Итог ( "СуммаИтогДоллар" );
	СуммаПлатежаДоллар = Транспорт.Итог ( "СуммаПлатежаДоллар" );
	СуммаДоходДоллар = Транспорт.Итог ( "СуммаДоходДоллар" );
	СуммаДоход = Транспорт.Итог ( "СуммаДоход" );
	СуммаРасходыДоллар = Транспорт.Итог ( "СуммаИтогДоллар" );
	СуммаРасходы = Транспорт.Итог ( "СуммаИтог" );
	СуммаРезультат = Транспорт.Итог ( "СуммаРезультат" );
	Если ( Контрагент = Справочники.Контрагенты.ПустаяСсылка () И Транспорт.Количество () > 0 ) Тогда
		Контрагент = Транспорт [ 0 ].Контрагент;  
	КонецЕсли; 
	заполнитьТранспортСтрокой ();
	
КонецПроцедуры

Процедура заполнитьТранспортСтрокой ()
	
	ТранспортСтрокой = "";
	Для Каждого строкаТЧ Из Транспорт Цикл
		ТранспортСтрокой = ТранспортСтрокой + ? ( ТранспортСтрокой = "", "", ", " ) + строкаТЧ.Транспорт;	
	КонецЦикла; 
	
КонецПроцедуры

Процедура заполнитьДополнительныеСвойства ()
	
	ДополнительныеСвойства.Вставить ( "Перепроведение", Проведен );
	ДополнительныеСвойства.Вставить ( "ОперативноеПроведение", ( ЭтоНовый () И НачалоДня ( Дата ) = НачалоДня ( ТекущаяДата () ) ) ); 
	
КонецПроцедуры 

Процедура ОбработкаПроведения ( Отказ, РежимПроведения )
	
	заполнитьДополнительныеСвойства	();
	данные = Новый Структура;
	данные.Вставить ( "Ссылка", Ссылка );
	данные.Вставить ( "Дата", Дата );
	данные.Вставить ( "Движения", Движения );
	данные.Вставить ( "МоментВремени", МоментВремени () );
	данные.Вставить ( "ДополнительныеСвойства", ДополнительныеСвойства );
	Отказ = НЕ Документы.СводныеДанные.Проведение ( данные );
	
КонецПроцедуры

Процедура ОбработкаЗаполнения ( ДанныеЗаполнения, СтандартнаяОбработка )
	
	Если ТипЗнч ( ДанныеЗаполнения ) = Тип ( "ДокументСсылка.Заявка" ) Тогда
		заполнитьПоЗаявке ( ДанныеЗаполнения );
	КонецЕсли;
	
КонецПроцедуры

Процедура заполнитьПоЗаявке ( ДанныеЗаполнения )
	
	стр = "
	|ВЫБРАТЬ Заявка.Организация КАК Организация, Заявка.Валюта КАК Валюта, 
	|	ЕСТЬNULL ( Курсы.Курс, 0 ) КАК Курс, СуммаПоДокументу как СуммаПоДокументу,
	|	КоличествоТранспорта как КоличествоТранспорта, Ссылка как Заявка
	|ИЗ Документ.Заявка КАК Заявка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних ( &Период ) КАК Курсы
	|	ПО Курсы.Валюта = Заявка.Валюта 
	|ГДЕ Заявка.Ссылка = &Ссылка
	|;
	|ВЫБРАТЬ 
	|	Ссылка КАК Заявка, Номенклатура КАК Номенклатура, ДатаЗакрытия как ДатаГТД,
	|	Контрагент КАК Контрагент, Порт КАК Порт, Вес КАК Количество, Транспорт КАК Транспорт, 
	|	ЦенаДоллар как ЦенаДоллар, Цена как Цена
	|ИЗ Документ.Заявка.Транспорт
	|ГДЕ Ссылка = &Ссылка  
	|";
	запрос = Новый Запрос ( стр );
	запрос.УстановитьПараметр ( "Ссылка", ДанныеЗаполнения );
	запрос.УстановитьПараметр ( "Период", ТекущаяДата () );
	результат = запрос.ВыполнитьПакет ();
	выборка = результат [ 0 ].Выбрать ();
	выборка.Следующий ();
	заполнитьШапку ( выборка );
	заполнитьТранспорт ( результат [ 1 ].Выбрать () );
	
КонецПроцедуры

Процедура заполнитьШапку ( Данные )
	
	Организация = Данные.Организация;
	Валюта = Данные.Валюта;
	Курс = Данные.Курс;
	Заявка = Данные.Заявка;
	
КонецПроцедуры

Процедура заполнитьТранспорт ( Выборка )
	 
	КоличествоТранспорта = Выборка.Количество ();
	Пока Выборка.Следующий () Цикл
		новаяСтрока = Транспорт.Добавить ();
		ЗаполнитьЗначенияСвойств ( новаяСтрока, Выборка );
		новаяСтрока.ЦенаДоллар = Выборка.ЦенаДоллар / КоличествоТранспорта;
		СуммаПоДокументуДоллар = Выборка.ЦенаДоллар;
		СуммаПоДокументу = Выборка.Цена;
		Контрагент = Выборка.Контрагент;
	КонецЦикла;
	
КонецПроцедуры