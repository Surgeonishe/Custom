
&НаСервере
Процедура ПриСозданииНаСервере ( Отказ, СтандартнаяОбработка )
	
	РольКассир = ( РольДоступна ( "Кассир" ) И НЕ РольДоступна ( "ПолныеПрава" ) );
	Если ( Объект.Ссылка.Пустая () ) Тогда
		начальноеЗаполнение ();
	Иначе
		// код ...	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура начальноеЗаполнение ()
	
	ОбщийДокументы.НачальноеЗаполнение ( Объект );
	Если ( ЗначениеЗаполнено ( Параметры.Основание ) ) Тогда
		// код ...
	Иначе
		Объект.Организация = Константы.ОсновнаяОрганизация.Получить ();
		Если ( РольКассир ) Тогда
			Объект.Касса = ПараметрыСеанса.ТекущийПользователь.ОсновнаяКасса;
		Иначе
			Объект.Касса = Константы.ОсновнаяКасса.Получить ();	
		КонецЕсли;
		Объект.ВидОперации = Перечисления.ВидОперацииРасходныйКассовыйОрдер.Оплата;
		Если ( Параметры.ЗначенияЗаполнения.Свойство ( "ВидОперации" ) ) Тогда
			Если ( ЗначениеЗаполнено ( Параметры.ЗначенияЗаполнения.ВидОперации ) ) Тогда
				Объект.ВидОперации = Параметры.ЗначенияЗаполнения.ВидОперации;
			КонецЕсли; 
		КонецЕсли;		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии ( Отказ )
	
	установитьВидимость ();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОперацииПриИзменении ( Элемент )
	
	установитьВидимость ();
	
КонецПроцедуры

&НаКлиенте
Процедура установитьВидимость ()
	
	Если ( Объект.ВидОперации = ПредопределенноеЗначение ( "Перечисление.ВидОперацииРасходныйКассовыйОрдер.Конвертация" ) ) Тогда
		Элементы.ГруппаСвязанныйДокумент.Видимость = Истина;
	ИначеЕсли ( Объект.ВидОперации = ПредопределенноеЗначение ( "Перечисление.ВидОперацииРасходныйКассовыйОрдер.ПеремещениеВКассу" ) ) Тогда
		Элементы.ГруппаСвязанныйДокумент.Видимость = Истина;
	Иначе
		Элементы.ГруппаСвязанныйДокумент.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатыКурсПриИзменении ( Элемент )
	
	данные = Элементы.Оплаты.ТекущиеДанные;
	Если ( Объект.Валюта = ПредопределенноеЗначение ( "Справочник.Валюты.Доллар" ) ) Тогда
		Если ( данные.Валюта = ПредопределенноеЗначение ( "Справочник.Валюты.Гривна" ) ) Тогда
			данные.Сумма = Объект.Сумма * данные.Курс;
		КонецЕсли;
	ИначеЕсли ( Объект.Валюта = ПредопределенноеЗначение ( "Справочник.Валюты.Гривна" ) ) Тогда
		Если ( данные.Валюта = ПредопределенноеЗначение ( "Справочник.Валюты.Доллар" ) И данные.Курс <> 0 ) Тогда
			данные.Сумма =Объект.Сумма / данные.Курс;
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи ( ПараметрыЗаписи )
	
	Оповестить ( "ИзменениеКассовыхДокументов", , Объект.Ссылка );	
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаПриИзменении ( Элемент )
	
	Если ( Объект.Оплаты.Количество () = 0 ) Тогда
		новая = Объект.Оплаты.Добавить ();
		новая.Валюта = Объект.Валюта;
		новая.Курс = 1;
		новая.Сумма = Объект.Сумма;
	Иначе
		строкаТЧ = Объект.Оплаты [ 0 ];
		Если ( строкаТЧ.Валюта = Объект.Валюта ) Тогда
			строкаТЧ.Сумма = Объект.Сумма; 
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатыСуммаПриИзменении ( Элемент )
	
	текущиеДанные = Элементы.Оплаты.ТекущиеДанные;
	Если ( текущиеДанные <> Неопределено ) Тогда
		Если ( текущиеДанные.НомерСтроки = 1 
			И текущиеДанные.Валюта = Объект.Валюта ) Тогда
			Объект.Сумма = текущиеДанные.Сумма; 
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры